
use uo;
use os;


program textcmd_goto( character, whom )

	whom := upper( whom );
	var chr := MakeAndProcessGump( character,whom);
	if( chr )
		MoveCharacterToLocation( character, chr.x, chr.y, chr.z, MOVECHAR_FORCELOCATION );
	endif

endprogram


function MakeAndProcessGump( who,whom)

	var layout	:= array( "page 0",
				  "resizepic 50  0   2600 300 525",
				  "resizepic 100 480 5100 200  25",
				  "text 155 30 5 0");

	var data	:= array( "Goto Menu");

	var playername;
	var lastindex:=0;
	var maxlen:=len(whom);
	var players	:= EnumerateOnlineCharacters();

	var start	:= 1;
	var count	:= 0;
	var done	:= 0;
	var page	:= 1;
	var perpage	:= 20;
	var y 	:= 70;
	var i;
	var j;
	var uhunter;

	layout.append( "page 1" );

	for( i := start; i <= len(players); i := i + 1 )
		uhunter:=CInt(GetObjProperty(players[i],"UHunter"));
		playername:=upper(players[i].name);


		if ((playername[1,maxlen]==whom) && ( (uhunter==0)|| ( who.cmdlevel>3 ) ))
			if( done == perpage )
				done 	:= 0;
				page 	:= page + 1;
				y 	:= 70;

				layout.append( "button 250 481 5540 5541 0 " + (page ) ); // Next button for prev page
				layout.append( "page " + page );
				layout.append( "button 125 481 5537 5538 0 " + (page - 1) ); // Prev button for current page
			endif

			if( ( players[i].cmdlevel <= who.cmdlevel ) && ( players[i].cmdlevel > 0 ) )

					layout.append( "text 85 " + y + " 51 " + len(data) );

					layout.append( "button 300 " + (y+5) + " 2118 2117 1 0 " + i );
					data.append( players[i].name );

					y 	:= y + 20;
					done 	:= done + 1;
					count := count + 1;
					lastindex:=i;
			endif
		endif
	endfor

	if (count>0)
		var result;
		if (count==1)
			result:=lastindex;
		else
			result := SendDialogGump( who, layout, data );
			if( !result[0] )
				return;
			endif
			result := CInt( result[0] );
		endif

		return players[result];
	else
		SendSysMessage(who, "No player with name '"+ whom +"*' was found.");
		return;
	endif
endfunction
