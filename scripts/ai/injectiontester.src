/////////////////////////////////////////////////////////////////////////////
//
// updated by Zulu (zuluhotel@home.com) Dec 24, 1999
// - fixed <invalid> error
// - fixed display of items and person
// - added hearing to npc, you no longer have to be standing on him
//   to talk to him.
// 
/////////////////////////////////////////////////////////////////////////////

use npc;
use os;
use uo;
use cfgfile;
use basic;

include "include/eventid";
include "include/randname";
include "include/npcbackpacks";
include "ai/setup/modsetup";
include "include/random";
var questlocs_cfg	:= ReadConfigFile( "questlocs" );
var questitems_cfg	:= ReadConfigFile( "questitems" );

var quest_type;
var item_name;
var has_item;
var has_loc;
var quest_reward;
var quest_gold;
var quest_item;
var noun;
var has_name;

const REACT_THRESHOLD := 2;

var me := Self();

if( me.name["<random>"] )
	SetName ( me , RandomName( me ) );
	EquipFromTemplate( me , "Clothes" );
endif

SetQuest();

program Questie()

	EnableEvents( SYSEVENT_SPEECH + SYSEVENT_ENGAGED + SYSEVENT_DISENGAGED + SYSEVENT_DAMAGED , 1);
	EnableEvents( SYSEVENT_OPPONENT_MOVED , 1);
	EnableEvents( SYSEVENT_ITEM_GIVEN , 1);
	EnableEvents( SYSEVENT_SPEECH , REACT_THRESHOLD );

	if( getobjproperty(me, "frozen") )
		me.frozen := 1;
	endif

	SetWarMode( 0 );

	var ev;
	while (1)
		ev := os::wait_for_event( 120 );

		case (ev.type)
			SYSEVENT_SPEECH:
				TurnToward( ev.source );
				if (ev.text[" ,"])
					PrintTextAbovePrivate( me, "That is incorrect.", ev.source );
					PrintTextAbovePrivate( me, "type ,injection", ev.source );
					PrintTextAbovePrivate( me, " ", ev.source );	
				elseif (ev.text = ",injection") 
					PrintTextAbovePrivate( me, "You Passed.", ev.source );
					PlayLightningBoltEffect( ev.source );
				elseif (ev.text[","])
					PrintTextAbovePrivate( me, "That is incorrect.", ev.source );
					PrintTextAbovePrivate( me, "type ,injection", ev.source );
					PrintTextAbovePrivate( me, " ", ev.source );	
				elseif (ev.text["injection"])
					PrintTextAbovePrivate( me, "That is incorrect.", ev.source );
					PrintTextAbovePrivate( me, "type ,injection", ev.source );
					PrintTextAbovePrivate( me, " ", ev.source );	
				elseif (ev.text["die"])
					if (ev.source.cmdlevel > 0)
						RevokePrivilege( me, "invul" );
						setobjproperty( me, "guardkill", 1);
						ApplyRawDamage( me, Cint(me.hp+3) );
					endif
				endif
			SYSEVENT_ENGAGED:
				Say ( ev.source.name + ", I'll call a guard, if you don't stop that!");

			SYSEVENT_DAMAGED:
				Say ("Help guards, I'm being attacked!");
				SetObjProperty(me,"guardkill",1);
				SetObjProperty(ev.source,"guardkill",1);
				me.hidden := 1;
				sleep(90);
				me.hidden := 0;

			SYSEVENT_ITEM_GIVEN:
				if( quest_type = 1 ) 
					if( ev.item.objtype = quest_item )
						DestroyItem(ev.item);
						Say( "Your kindness shall not be forgotten. Here is a small token of my thanks." );
						GiveReward(ev.source);
						RestartScript(me);
					else
						MoveItemToContainer(ev.item, ev.source.backpack);
						Say( "I have no use of this!" );
					endif
					
			   	elseif( GetObjProperty(ev.item, "iWant") = me.serial ) 
					DestroyItem(ev.item);
					Say("Your kindness shall not be forgotten. Here is a small token of my thanks.");
					GiveReward(ev.source);
					RestartScript(me);
				else
					MoveItemToContainer(ev.item, ev.source.backpack);
					Say( "I have no use of this!" );
				endif
		endcase
	endwhile
	
endprogram

function SetQuest()
// type 1 : regular item in store
// type 2 : stolen from me
// type 3 : I want stolen
	if( getobjproperty( me, "quest_type" ) )
		quest_type	:= GetObjProperty( me, "quest_type");
		item_name	:= GetObjProperty( me, "itemname");
		has_item	:= GetObjProperty( me, "has_item");
		has_loc		:= GetObjProperty( me, "has_loc");
		quest_reward	:= GetObjProperty( me, "reward");
		quest_gold	:= GetObjProperty( me, "quest_gold");
		quest_item	:= GetObjProperty( me, "quest_item");
		noun		:= GetObjProperty( me, "noun");
		has_name	:= GetObjProperty( me, "has_name");
		
	else
		quest_type	:= Random(3) + 1;
		
		// For the location
		var maxelem	:= GetConfigMaxIntKey( questlocs_cfg );   
		var telem	:= Random( maxelem ) + 1;
		var qelem	:= FindConfigElem( questlocs_cfg , telem );
		if( !qelem )
			return;
		endif
		
		var x		:= GetConfigInt( qelem , "x" );
		var y		:= GetConfigInt( qelem , "y" );
		var xrange	:= Random( GetConfigInt( qelem , "rangex" ) ) + 1;
		var yrange	:= Random( GetConfigInt( qelem , "rangey" ) ) + 1;
		has_loc		:= GetConfigString( qelem , "name" );

		
		// For the item
		maxelem		:= GetConfigMaxIntKey( questitems_cfg );
		telem		:= Random( maxelem ) + 1;
		qelem		:= FindConfigElem( questitems_cfg , telem );
		qelem		:= findconfigelem(questitems_cfg, telem);
		if( !qelem )
			return;
		endif
		
		quest_item	:= GetConfigInt( qelem , "itemid" );
		quest_reward	:= GetConfigInt( qelem , "reward" );
		quest_gold	:= GetConfigInt( qelem , "gold" );
		item_name	:= GetConfigString( qelem , "message" );
		noun		:= GetConfigString( qelem , "noun" );

		if( quest_type != 1 ) // Item held by a npc
			var tbaddie := CreateNpcFromTemplate( "quest_target" , x , y , 0 );
			SetName( tbaddie , RandomName( me ) );
			
			var hispack := tbaddie.backpack;

			if (!hispack)
				hispack := CreateItemAtLocation( tbaddie.x , tbaddie.y , tbaddie.z , 0x0e75 , 1 );
				EquipItem( tbaddie , hispack );
			endif

			var titem := CreateItemInBackpack( tbaddie , quest_item , 1 );
			
			SetObjProperty( titem , "iWant" , me.serial );
			if( quest_type = 2 ) 
				titem.name := me.name + "'s " + noun;
			else
				titem.name := tbaddie.name + "'s " + noun;
			endif
			
			has_name := tbaddie.name;
		endif

		SetObjProperty( me, "quest_type" , quest_type );			
		SetObjProperty( me, "itemname"	 , item_name );				
		SetObjProperty( me, "has_item"   , has_item );				 
		SetObjProperty( me, "has_loc"    , has_loc );					
		SetObjProperty( me, "reward"     , quest_reward );				
		SetObjProperty( me, "quest_gold" , quest_gold );			 
		SetObjProperty( me, "quest_item" , quest_item );			 
		SetObjProperty( me, "noun"       , noun );						 
		SetObjProperty( me, "has_name"   , has_name );				 
	endif

endfunction


function GiveReward( who )

	if( quest_reward )
		CreateItemInBackpack( who , quest_reward );
	endif
	
	if( quest_gold ) 
		CreateItemInBackpack( who , 0xeed , quest_gold );
	endif
	
	ResetQuest();

endfunction


function ResetQuest()

	EraseObjProperty( me, "quest_type");
	EraseObjProperty( me, "itemname");
	EraseObjProperty( me, "has_item");
	EraseObjProperty( me, "has_loc");
	EraseObjProperty( me, "reward");
	EraseObjProperty( me, "quest_gold");
	EraseObjProperty( me, "quest_item");
	EraseObjProperty( me, "noun");
	EraseObjProperty( me, "has_name");

	quest_type	:= 0;
	item_name	:= 0;
	has_item	:= 0;
	has_loc		:= 0;
	quest_reward	:= 0;
	quest_gold	:= 0;
	quest_item	:= 0;
	noun		:= 0;
	has_name	:= 0;

endfunction