use uo;

include "include/constants/propids";
include "include/managers";

program DestroySpawnPoint(point)

	var pt_data := GetObjProperty( point, PROPID_SPAWNPOINT_SETTING );
	if( !pt_data )
		pt_data	    := {};
		pt_data[1]  := "NPC";	// Type
		pt_data[2]  := "nil";	// Template
		pt_data[3]  := 0;	// Max appear
		pt_data[4]  := 0;	// Appear Range
		pt_data[5]  := 0;	// Wander Range
		pt_data[6]  := 0;	// Frequency
		pt_data[7]  := 0;	// Disabled
		pt_data[8]  := 0;	// Spawn in group.
		pt_data[9]  := 0;	// Despawn on destroy
		pt_data[10] := 0;	// Expire time
		pt_data[11] := 0;	// Expire number
		pt_data[12] := 0;	// Start spawning hours
		pt_data[13] := 0;	// End spawning hours
		pt_data[14] := "";	// Notes
	endif
	
	if( pt_data[9] )
		var spawned := GetObjProperty(point, PROPID_SPAWNPOINT_SPAWNED_OBJECTS);
		if( spawned )
			var spawned_object;
			foreach spawned_serial in spawned
				spawned_object := SystemFindObjectBySerial(spawned_serial);
				case( pt_data[1] )
					"NPC":
					"Group":	RevokePrivilege(spawned_object,"invul");
							SetObjProperty(spawned_object, "guardkill", 1);
							ApplyRawDamage(spawned_object, spawned_object.hp+3);

					"Container":	DestroyItem(spawned_object);
					
					"Item":		if( spawned_object )
								if( !spawned_object.container )
									DestroyItem(spawned_object);
								endif
							endif
				endcase
			endforeach
		endif
	endif
			
	return 1;
	
endprogram
