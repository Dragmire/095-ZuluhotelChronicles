use uo;
use os;


include "include/client";
include "include/spelldata";
include "include/rituals";
include "include/string";

const LAYER_START := 1;
const LAYER_END	  := 25;

program rconsecration( parms )

	var caster;
	var magic_circle;
	var item;
	var i;

	if (parms[1] == "#MOB")
		caster := parms[2];
	else
		caster := parms;
	endif


	//Check if the caster have some unwanted equipment
	for( i := LAYER_START; i <= LAYER_END; i := i + 1 )
		var equiped_item := GetEquipmentByLayer( caster, i );
		if( i != 9 && i != 11 && i != 15 && i != 16 && i != 21 && equiped_item && equiped_item.objtype != RITUAL_ROBE && equiped_item.objtype != OAK_STAFF )
			SendSysMessage( caster, "You can't perform this ritual clothed like this!" );
			EraseObjProperty( caster, "#Casting" );
			return;
		endif
	endfor

	magic_circle := LookForTheCircle( caster );
	if( !magic_circle )
		EraseObjProperty( caster, "#Casting" );
		EndRitual( caster, captor );
		return;
	endif


	//Ask for the item to consecrate
	while( 1 )
		SendSysMessage( caster, "What item do you wish to consecrate?" );
		var the_item := Target( caster, TGTOPT_CHECK_LOS );
		if( !the_item )
			SendSysMessage( caster, "The ritual is aborted" );
			EraseObjProperty( caster, "#Casting" );
		
			if( GetObjProperty( magic_circle[1], "PermMagicCircle" ))
				var i;
				for( i := 5; i <= len(magic_circle); i := i + 1 )
					DestroyItem( magic_circle[i] );
				endfor
			else
				TryToUndoTheCircle( caster, magic_circle );
			endif
			return;
		elseif( !Accessible( caster, the_item ) )
			SendSysMessage( caster, "You can't reach that" );
		elseif( the_item.objtype != RITUAL_ROBE && the_item.objtype != OAK_STAFF )
			SendSysMessage( caster, "You can only consecrate a Ritual Robe or a Young Oak Staff" );
		else
			item := the_item;
			break;
		endif
	endwhile

	
	//Try to perform the ritual
	if( !ProcessRitual( caster, item, magic_circle, SPELLID_RITUAL_OF_CONSECRATION ) )
		EndRitual( caster, captor );
		return;
	endif

	SetObjProperty( item, "Consecrated", caster.serial );
	SetName( item, caster.name + "'s " + TruncateArticle(item.desc) );
	item.newbie := 1;

endprogram