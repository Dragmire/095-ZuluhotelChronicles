//  CTF by Zulu  zulu@zuluhotel.com  created February 15, 2000
//  update: November 14, 2000

use uo;
use os;

include "include/eventid";
include "include/client";
include "ctf1";
include "ctf2";

program startgame()

SetGlobalProperty("ctfredcaps",0);
SetGlobalProperty("ctfbluecaps",0);
SetGlobalProperty("ctfredteam",0);
SetGlobalProperty("ctfblueteam",0);
SetGlobalProperty("ctfbluescore",0);
SetGlobalProperty("ctfredscore",0);
SetGlobalProperty("#ctfgame",0);
SetGlobalProperty("#ctfplyr",{"NONE"});

var totalplayers,blueflag,redflag,points;

while(1)

  totalplayers:=0;

  while(totalplayers<2)
    totalplayers:=howmany();
    if (2-totalplayers>0)
    messagectf((2-totalplayers)+" more player(s) to start.",55);
    endif
    sleep(20);
  endwhile

  messagectf("Get Ready, CTF is starting...",70,307);
  game_start();
  SetGlobalProperty("#ctfgame",1);

  while(GetGlobalProperty("#ctfgame")==1)

    blueflag:=SystemFindObjectBySerial(CInt(GetGlobalProperty("#bflag")));

    if (not GetObjProperty(blueflag,"b"))
      fix_flag({6119,358,-22},"BLUE");
      SetGlobalProperty("ctfblueloc","home");
      messagectf("Blue flag has been returned to base.",300,0);
    endif

    redflag:=SystemFindObjectBySerial(CInt(GetGlobalProperty("#rflag")));

    if (not GetObjProperty(redflag,"r"))
      fix_flag({6038,358,44},"RED");
      SetGlobalProperty("ctfredloc","home");
      messagectf("Red flag has been returned to base.",33,0);
    endif

    if (GetGlobalProperty("ctfredloc")!="home")
    PlaySoundEffect(redflag,306);
    endif

    if (GetGlobalProperty("ctfblueloc")!="home")
    PlaySoundEffect(blueflag,306);
    endif

    sleep(20);

    if (howmany()==0)
      game_end();
      break;
    endif

  endwhile

endwhile

endprogram