use uo;
use os;
use cfgfile;

var cfg	:= ReadConfigFile( "::guardedareas" );
var areas	:= GetConfigStringArray( cfg["1"] , "Area" );
var disabled	:= GetGlobalProperty( "DisabledRecallingAreas" );

program recallingareas( who )


	var layout	:= array( "page 0",
				  "resizepic 50  0   2600 250 325",
				  "resizepic 100 280 5100 150 25",
				  "text 110 30 5 0" );
				
	var data	:= array( "Recalling Areas" );

	if( !disabled )
		disabled := {};
	endif
	
	var name;
	var start	:= 1;
	var count	:= 0;
	var done	:= 0;
	var page	:= 1;
	var perpage	:= 10;
	var y 	:= 70;
	var i;
	var j;

	layout.append( "page " + page );
	if( len(areas) > perpage )
		layout.append( "button 200 281 5540 5541 0 " + (page + 1) );
	endif
	
	for( i := start ; i <= len(areas) ; i := i + 1 )
		if( done = perpage )
			done 	:= 0;
			page 	:= page + 1;
			y 	:= 70;
			
			layout.append( "page " + page );
			layout.append( "button 125 281 5537 5538 0 " + (page - 1) );
			if( len(areas) > (count + perpage) )
				layout.append( "button 200 281 5540 5541 0 " + (page + 1) );
			endif
		endif
		
		var splited := SplitWords( areas[i] );
		name := splited[5];
		j := 6;
		while( splited[j] )
			name := name + " " + splited[j];
			j := j + 1;
		endwhile
			
		layout.append( "text 85 " + y + " 0 " + len(data) );
		
		if( name in disabled )
			layout.append( "button 250 " + y + " 210 211 1 0 " + (i + 1000) );
		else
			layout.append( "button 250 " + y + " 211 210 1 0 " + i );
		endif
		
		data.append( name );

		y 	:= y + 20;
		done 	:= done + 1;
		count 	:= count + 1;
	endfor
			
	var result := SendDialogGump( who , layout , data );
	if( !result[0] )
		return;
	endif
	
	result := Cint( result[0] );
	
	if( result < 1000 )
		DisableArea( who , result );
	else
		EnableArea( who , (result - 1000) );
	endif

endprogram


function DisableArea( who , result )

	var splited	:= SplitWords( areas[result] );
	var name	:= splited[5];
	var i := 6;
	while( splited[i] )
		name := name + " " + splited[i];
		i := i + 1;
	endwhile
	
	disabled.append( name );
	
	if( SetGlobalProperty( "DisabledRecallingAreas" , disabled ) )
		SendSysMessage( who , "Area " + name + " was successfully disabled." );
	endif
	
endfunction


function EnableArea( who , result )

	var splited	:= SplitWords( areas[result] );
	var name	:= splited[5];
	var i := 6;
	while( splited[i] )
		name := name + " " + splited[i];
		i := i + 1;
	endwhile
	
	var newarray := {};
	
	foreach disabled_name in disabled
		if( disabled_name != name )
			newarray.append( disabled_name );
		endif
	endforeach

	if( len( newarray ) )
		if( SetGlobalProperty( "DisabledRecallingAreas" , newarray ) )
			SendSysMessage( who , "Area " + name + " was successfully enabled." );
		endif
	else
		if( EraseGlobalProperty( "DisabledRecallingAreas" ) )
			SendSysMessage( who , "Area " + name + " was successfully enabled." );
		endif
	endif
	
endfunction