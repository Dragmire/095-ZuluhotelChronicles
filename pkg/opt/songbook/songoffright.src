use uo;

include "include/spelldata";
include "include/client";
include "include/objtype";
include "include/skillpoints";
include "include/bard";
include "include/eventid";
include "include/checkcity";
include "include/dotempmods";
const SPELL_EFFECT_TYPE_MOVING 	:= MOVING_SPELL_PARALYZE;
const SPELL_EFFECT_ID		:= FX_SPELL_PARALYZE;
const SPELL_EFFECT_SPEED	:= SPEED_SPELL_PARALYZE;
const SPELL_EFFECT_LOOP		:= LOOP_SPELL_PARALYZE;
program cast_owlsight( player )
	var singinghp := GetHp( player );
	var configfile := ReadConfigFile("::npcdesc");
	var elem;
	SetObjProperty( player , "#Casting", 1 );
	var circle := GetCircle( SPELLID_SONG_OF_FRIGHT  );
	var caster := player;
	var cast_on := player;
	var instrument := findinstrument( player );
	var sfx_success;
	var sfx_failure;
	var elem2;
	case( instrument.graphic )
		UOBJ_HARP:	sfx_success := SFX_HARP_SUCC;
				sfx_failure := SFX_HARP_FAIL;
				break;
				
		UOBJ_LUTE:	sfx_success := SFX_LUTE_SUCC;
				sfx_failure := SFX_LUTE_FAIL;
				break;
				
		UOBJ_DRUM:	sfx_success := SFX_DRUM_SUCC;
				sfx_failure := SFX_DRUM_FAIL;
				break;
				
		UOBJ_TAMB:	sfx_success := SFX_TAMB_SUCC;
				sfx_failure := SFX_TAMB_FAIL;
				break;
				
		default:	PrintTextAbovePrivate( player, "You dont have an Instrument!",player );
				EraseObjProperty( player , "#Casting" );
				return;
	endcase
	

	var peace := GetAttribute(caster, ATTRIBUTEID_PEACEMAKING);
	var mus := GetAttribute(caster, ATTRIBUTEID_MUSICIANSHIP);
	var spec := GetObjProperty(player,"IsBard");
	var ps;
	if (!spec)
		spec := 0;	
	endif
	var dmod;
	var duration;
	var proped := array();
	var drain := 10 + (circle*2)-(spec+5);
	var difficulty := (circle*12)+(Random(25)-(spec*8));
	var affected;
	//PrintTextAbove(player,"Difficulty:"+difficulty);
	PerformAction( player, 0X021 );
if (difficulty > peace or difficulty > mus or !spec)
		PlaySoundEffect( caster , sfx_failure );
		PrintTextAbove(player,"*"+player.name+" plays a song poorly*");
		ps := GetStamina( player ) -(drain/2);
		if (ps < 0 )
			ps := 0;
		endif
		GetStamina( player ) := ps;
		EraseObjProperty( caster , "#Casting" );
		return;
	elseif ((GetStamina( player ) -drain) <  0)
		PrintTextAbove(player,"*"+player.name+" plays a song poorly*");
		PrintTextAbovePrivate( player, "Out of Stamina!",player );
		PlaySoundEffect( caster , sfx_failure );
		EraseObjProperty( caster , "#Casting" );
		return;
	else		
		ps := GetStamina( player ) -drain;
		if (ps < 0 )
			ps := 0;
		endif
		GetStamina( player ) := ps;
		sleep(2);
		if (GetHp( player ) < singinghp)
			PrintTextAbove(player,"*"+player.name+" plays a song poorly*");
			PrintTextAbovePrivate( player, "You were interrupted!",player );
			PlaySoundEffect( caster , sfx_failure );
			EraseObjProperty( caster , "#Casting" );
			return;
		else
			PerformAction( player, 0X021 );
			PlaySoundEffect( caster , sfx_success );
			PrintTextAbove(player,"Refugio Iniquus Abutor Reverentia");
			sleep(2);
			dmod := (peace/10) + spec*5;
			duration := (peace/260)+(spec*2);
			EraseObjProperty( caster , "#Casting" );
			affected := ListMobilesNearLocation( cast_on.x , cast_on.y , cast_on.z , cint( (spec+1) * 2 ) );
			foreach cast_near in affected
				elem2 := FindConfigElem( configfile, cast_near.npctemplate );
			if(cast_near.npctemplate and cast_near.script != ":songbook:frightai" and elem2.Privs != "invul" )
					elem := FindConfigElem( configfile, cast_near.npctemplate );
					difficulty := Cint( elem.provoke );
					if( CheckSkill( player, SKILLID_PEACEMAKING, difficulty, 0 ) )
						PrintTextAbove(cast_near, "*"+cast_near.name+" flees in fright!*"); 
						SetObjProperty(cast_near, "originalscript",cast_near.script);
					  	SetObjProperty(cast_near, "frightlength", duration);
					  	SetObjProperty(cast_near, "runfrom",player.serial);
						cast_near.script := ":songbook:frightai";
						RestartScript(cast_near);
					endif
				endif
			endforeach
		endif
	endif
	EraseObjProperty( caster , "#Casting" );
endprogram
