use uo;
use os;

include "include/spelldata";
include "include/classes";
include "include/client";
include "include/random";
include "include/damages";

const SPELL_EFFECT_TYPE_MOVING 	:= MOVING_SPELL_NATURES_TOUCH;
const SPELL_EFFECT_ID		:= FX_SPELL_NATURES_TOUCH;
const SPELL_EFFECT_SPEED	:= SPEED_SPELL_NATURES_TOUCH;
const SPELL_EFFECT_LOOP		:= LOOP_SPELL_NATURES_TOUCH;

program cast_natures_touch( parms )

	var circle := GetCircle( SPELLID_NATURES_TOUCH );
	var caster;
	var cast_on;
	var fromhit;

 	if (parms[1] == "#MOB")
		caster := parms[2];
		cast_on := parms[3];
		if (parms[4])
			circle := parms[4];
		endif
		if (parms[5])
			fromhit := parms[5];
		endif

	else
		caster := parms;
		if( TryToCast( caster, SPELLID_NATURES_TOUCH ) != SUCCESS )
			EraseObjProperty( caster, "#Casting" );
			return;
		endif
		cast_on := CanTargetSpell( caster, circle, TGTOPT_HELPFUL + TGTOPT_CHECK_LOS );
	
		if( !cast_on )
			EraseObjProperty( caster, "#Casting" );
			return;
		endif
	endif

	if( !fromhit )
		EraseObjProperty( caster, "#Casting" );
		cast_on := Reflected( caster, cast_on, circle );
	else
		PlayObjectCenteredEffect(cast_on,
					 SPELL_EFFECT_ID,
					 SPELL_EFFECT_SPEED,
					 SPELL_EFFECT_LOOP);
	endif

	PlaySoundEffect( cast_on, SFX_SPELL_NATURES_TOUCH );

	var healed := ModifyWithMagicEfficiency( caster, RandomDiceStr( "6d8+30" ) );
	var bonus := GetObjProperty( cast_on, "HealingIncrease" );
	if( bonus )
		healed := healed + CInt( healed * bonus * 0.1 );
	endif

	if( healed < 0 )
		healed := 0;
	endif

	HealTheDamage( cast_on, healed );

	EraseObjProperty( caster, "#Casting" );

endprogram
