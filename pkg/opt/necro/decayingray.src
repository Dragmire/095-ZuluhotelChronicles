use uo;


include "include/spelldata";
include "include/dotempmods";
include "include/client";

const SPELL_EFFECT_TYPE_MOVING 	:= MOVING_SPELL_DECAYING_RAY;
const SPELL_EFFECT_ID		:= FX_SPELL_DECAYING_RAY;
const SPELL_EFFECT_SPEED	:= SPEED_SPELL_DECAYING_RAY;
const SPELL_EFFECT_LOOP		:= LOOP_SPELL_DECAYING_RAY;

program decayingray( parms )

	var circle := GetCircle( SPELLID_DECAYING_RAY ) + 1;
	var plane := NECRO;
	var caster;
	var cast_on;
	var fromhit;

	if (parms[1] == "#MOB")
		caster := parms[2];
		cast_on := parms[3];
		if (parms[4])
			circle := parms[4];
		endif
		if (parms[5])
			fromhit := parms[5];
		endif

	else
		caster := parms;
		if( TryToCast( caster, SPELLID_DECAYING_RAY ) != SUCCESS )
			EraseObjProperty( caster, "#Casting" );
			return;
		endif
		SendSysMessage(caster, "Select a target.");
		cast_on := CanTargetSpell( caster, circle, TGTOPT_CHECK_LOS + TGTOPT_HARMFUL );		
		if( !cast_on )
			EraseObjProperty( caster, "#Casting" );
			return;
		endif
	endif

	if( !fromhit )
		EraseObjProperty( caster, "#Casting" );
		cast_on := Reflected( caster, cast_on, circle );
	endif

        PlaySoundEffect(caster, SFX_SPELL_DECAYING_RAY );

	var immunity := IsProtected( caster, cast_on, circle );
	if( immunity == IMMUNED )
		EraseObjProperty( caster, "#Casting" );
		return;
	endif

	if( !CanMod( cast_on, "ar" ) )
		SendSysMessage( caster, "The target have a spell on him that protect him from the spell effect." );
		EraseObjProperty( caster, "#Casting" );
		return;
	endif


	var amount := CInt( GetModAmount( caster ) * 1.25 );
	var duration := GetModDuration( caster );
	var protection := GetResultingPlaneProtection( cast_on, plane );

	if( protection )
		var modifier := 100 - (protection * 20);
		if( modifier < 1 )
			modifier := 0;
		endif
		
		amount := CInt( amount * modifier / 100 );
		duration := CInt( duration * modifier / 100 );

		if( !duration || !amount )
			PrintTextAbovePrivate( cast_on, "The nature of the target prevent it from being affected by that spell!", caster );
			EraseObjProperty( caster, "#Casting" );
			return;
		endif
	endif

	amount := Resisted( caster, cast_on, circle, amount );
	duration := Resisted( caster, cast_on, circle, duration );

	if( !amount )
		return;
	endif

        DotempMod( cast_on, "ar", 0 - amount, duration );
        SendSysMessage( caster, "You reduced " + cast_on.name + "'s armor rating by " + amount + "." );
        SendSysMessage( cast_on, "Your armor rating was reduced by " + amount + "." );
        EraseObjProperty( caster, "#Casting" );

endprogram