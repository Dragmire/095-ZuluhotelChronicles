// ********************************************************************************
// ***
// ***	magic fish script
// ***
// ***	This script is called each time a player uses (double-clicks) a magic fish
// ***
// ********************************************************************************

// ********************************************************************************
//	function modules that will be used
// ********************************************************************************
use uo;
use os;

// ********************************************************************************
//	included modules that will be used
// ********************************************************************************
//include "include/statmod";
include "include/dotempmods";

// ********************************************************************************
//	Main program
//	
//	This is the main magic fish handler.  It is called when the player
//	double-clicks on a magic fish.
// ********************************************************************************
program use_magicfish( who, fish )
	// indicate the bottle is in use
	if(!ReserveItem(fish)) return; endif

	// get the cprops we need before destroying the fish
	var spellnum := GetObjProperty( fish, "spellnum" );
	var spellsound := GetObjProperty( fish, "spellsound" );
	var spelleffect := GetObjProperty( fish, "spelleffect" );
	var miscprop1 := GetObjProperty( fish, "miscprop1" );
	var miscprop2 := GetObjProperty( fish, "miscprop2" );
	subtractamount(fish, 1);
	if (spellnum > 0)
		// this magic fish has a spell set for it
		// play the sound and animations that are requested
		PlaySoundEffect( who, spellsound );
		PlayObjectCenteredEffect( who, spelleffect, 10,10);
		// debug messages
		// SendSysMessage( who, "Fish has spell number " + spellnum);
		// SendSysMessage( who, "Using spell sound " + spellsound);
		// SendSysMessage( who, "Using spell effect" + spelleffect);
		case (spellnum)		// handle the different magical effects

			4:	// heal
				HealDamage( who, miscprop1);

			6:	// night sight
				who.setlightlevel( miscprop1, miscprop2 );

			9:	// agility
				if (CanMod(who, "dex"))
					DoTempMod(who, "dex", miscprop1, miscprop2);
				endif

			10:	// cunning
				if (CanMod(who, "int"))
					DoTempMod(who, "int", miscprop1, miscprop2);
				endif

			11:	// cure
				if (who.poisoned)
				PlayObjectCenteredEffect( who, FX_BLESS_EFFECT, 10,10);
				CurePoison(who,SFX_SPELL_CURE,"The fish has CURED you!");
				else
				SendSysMessage(who, "You are not poisoned!");
				endif
			16:	// strength
				if (CanMod(who, "str"))
					DoTempMod(who, "str", miscprop1, miscprop2);
				endif

			17:	// bless
				if (CanMod(who, "all"))
					DoTempMod(who, "all", miscprop1, miscprop2);
				endif

			41:	// 	Dispel
				WipeMods( who );
				if( who.graphic != who.trueobjtype and !who.dead )
					who.graphic := who.trueobjtype;
				endif
				SendSysmessage( who , "All the magical effects active on you have been wiped." );

		    44:	// invisibility
				who.hidden := 1;

		endcase
	else
		// bad spellnum cprop on this fish, it is 0 (not valid)
		SendSysMessage(who, "Yech, that was a rotten fish!");
	endif
endprogram
