use uo;
use os;

include "include/dotempmods";
include "include/spelldata";
include "include/client";

const SPELL_EFFECT_TYPE_MOVING 	:= MOVING_SPELL_CUNNING;
const SPELL_EFFECT_ID		:= FX_SPELL_CUNNING;
const SPELL_EFFECT_SPEED	:= SPEED_SPELL_CUNNING;
const SPELL_EFFECT_LOOP		:= LOOP_SPELL_CUNNING;

program cast_CUNNING( parms )

	var circle := 2;
	var caster;
	var cast_on;
	var fromhit;

	if (parms[1] == "#MOB")
		caster := parms[2];
		cast_on := parms[3];
		if (parms[4])
			circle := parms[4];
		endif
		if (parms[5])
			fromhit := parms[5];
		endif

	else
		caster := parms;
		cast_on := CanTargetSpell(caster, circle, TGTOPT_HELPFUL + TGTOPT_CHECK_LOS );
		if (!cast_on)
			return;
		endif
	endif

	if( !fromhit )
		Detach();
		cast_on := Reflected( caster, cast_on, circle );
	endif
		PlayObjectCenteredEffect(cast_on,
                                         SPELL_EFFECT_ID,
                                         SPELL_EFFECT_SPEED,
                                         SPELL_EFFECT_LOOP);
	PlaySoundEffect(cast_on, SFX_SPELL_CUNNING);

	if (!CanMod(cast_on, "int"))
		SendSysMessage(caster,"Already under the influence!");
		return;
	endif

	var mod_amount := GetModAmount( caster );
	var duration := GetModDuration( caster );

	DoTempMod(cast_on, "int", mod_amount, duration);

endprogram
