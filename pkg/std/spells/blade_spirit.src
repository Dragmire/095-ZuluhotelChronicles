use uo;
use os;

include "include/spelldata";
include "include/attributes";
include "include/classes";
include "include/client";

program cast_summon( parms )

	var circle := 5;
	var caster;
	var cast_loc;
	var fromhit;

	if (parms[1] == "#MOB")
		caster := parms[2];
		cast_loc := parms[3];
		if (parms[4])
			circle := parms[4];
		endif
		if (parms[5])
			fromhit := parms[5];
		endif

	else
		caster := parms;
		cast_loc := CanTargetArea(caster, circle);
		if (!cast_loc)
			return;
		endif
	endif

	var thecreature := "bladespirit";
	var thecritter := CreateNpcFromTemplate(thecreature, cast_loc.x, cast_loc.y,cast_loc.z);

	SetObjProperty(thecritter, "summoned",1);
	SetObjProperty(thecritter, "master",caster.serial);
	PlaySoundEffect(thecritter,SFX_SPELL_BLADE_SPIRIT);
	thecritter.setmaster( thecritter );

	var ev array;
	ev.+type := 0x0002;
	ev.+source := 0;

	foreach critter in ListMobilesInLineOfSight( thecritter, 5 )
                ev.source := critter;
		SendEvent(thecritter, ev);
	endforeach

	var duration := CInt(GetEffectiveSkill( caster, SKILLID_MAGERY ) / 3);
	if( GetObjProperty( caster, CLASSEID_MAGE ) )
		duration := CInt( duration * CLASSE_BONUS );
	endif

	Detach();

	sleep( duration );

	if (thecritter.x)
	PlaySoundEffect( thecritter, SFX_SPELL_BLADE_SPIRIT );
	PlayStationaryEffect( CInt(thecritter.x), CInt(thecritter.y), CInt(thecritter.z), FX_SPELL_BLADE_SPIRIT, SPEED_SPELL_BLADE_SPIRIT, LOOP_SPELL_BLADE_SPIRIT );
	MoveCharacterToLocation(thecritter,5288,1176,0,MOVECHAR_FORCELOCATION);
	ApplyRawDamage(thecritter, GetHp(thecritter) + 3);
	endif

endprogram

