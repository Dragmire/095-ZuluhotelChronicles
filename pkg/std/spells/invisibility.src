use uo;
use os;

include "include/spelldata";
include "include/attributes";
include "include/client";

const SPELL_EFFECT_TYPE_MOVING 	:= MOVING_SPELL_INVISIBILITY;
const SPELL_EFFECT_ID		:= FX_SPELL_INVISIBILITY;
const SPELL_EFFECT_SPEED	:= SPEED_SPELL_INVISIBILITY;
const SPELL_EFFECT_LOOP		:= LOOP_SPELL_INVISIBILITY;

program cast_invis( parms )

	var circle := 6;
	var caster;
	var cast_on;
	var fromhit;

	if (parms[1] == "#MOB")
		caster := parms[2];
		cast_on := parms[3];
		if (parms[4])
			circle := parms[4];
		endif
		if (parms[5])
			fromhit := parms[5];
		endif

	else
		caster := parms;
		cast_on := CanTargetSpell(caster, circle, TGTOPT_HELPFUL + TGTOPT_CHECK_LOS );
		if (!cast_on)
			return;
		endif
	endif

	if( !fromhit )
		Detach();
		cast_on := Reflected( caster, cast_on, circle );
	endif

        PlaySoundEffect(caster, SFX_SPELL_INVISIBILITY);
        PlaySoundEffect(cast_on, SFX_SPELL_INVISIBILITY);
        cast_on.hidden := 1;

	Detach();
	sleep(ModifyWithMagicEfficiency( caster, CInt(GetEffectiveSkill(caster, SKILLID_MAGERY))));
        cast_on.hidden := 0;

endprogram