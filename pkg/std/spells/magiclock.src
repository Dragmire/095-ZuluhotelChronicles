// Magic lock and Magic unlock  version .4
// by Zulu (zulu@zuluhotel.com)   July 5, 2000

use os;
use uo;

include "include/spelldata";
include "include/attributes";
include "include/classes";
include "include/client";
include "util/key";
include "include/checkcity";

program cast_strength( parms )

	var circle := 3;
	var caster;
	var cast_on;
	var fromhit;

	if (parms[1] == "#MOB")
		caster := parms[2];
		cast_on := parms[3];
		if (parms[4])
			circle := parms[4];
		endif
		if (parms[5])
			fromhit := parms[5];
		endif

	else
		caster := parms;
		cast_on := CanTargetThing(caster, circle, TGTOPT_HELPFUL + TGTOPT_CHECK_LOS );
		if (!cast_on)
			return;
		endif
	endif

	if( !cast_on.isa( POLCLASS_LOCKABLE ) )
		SendSysMessage( caster, "That can't be locked." );
		return;
	endif

	if (cast_on.objtype == UOBJ_CORPSE)
		SendSysMessage( caster, "That can't be locked.");
		return;
	endif

	if ((cast_on.multi) || (GetObjProperty(cast_on,"houseserial")) || (GetObjProperty(cast_on,"house_serial")))
		SendSysMessage(caster, "That cant be magic locked!");
		PlayLightningBoltEffect(caster);
		return;
	endif

	if (cast_on.locked)
		SendSysMessage(caster,"That is already locked!");
		return;
	endif

	if ((checkfordoors(cast_on)) && (checkcity(cast_on)))
		SendSysMessage(caster, "That cant be magic locked!");
		return;
	endif

	var magery	:= GetEffectiveSkill( caster, SKILLID_MAGERY );
	var lockdiff	:= magery;
	if( GetObjProperty( caster, CLASSEID_MAGE ) )
		lockdiff := CInt( lockdiff * CLASSE_BONUS );
	endif

	PlaySoundEffect( cast_on, SFX_SPELL_MAGIC_LOCK1 );
	SetObjProperty( cast_on, "LockDifficulty", lockdiff );
	SetObjProperty( cast_on, "lockid", AllocLockId() );
	EraseObjProperty( cast_on, "spawnchest");
	PrintTextAbovePrivate( cast_on, "*Locked*", caster );
	cast_on.locked := 1;

endprogram


function checkfordoors(cast_on)

	if( (cast_on.graphic<=0x1ffc) && (cast_on.graphic>=0x1fed) ||
    	    (cast_on.graphic<=0x06f4) && (cast_on.graphic>=0x0675) )
		return 1;
	endif

	return 0;

endfunction