use os;
use uo;
use boat;

const STATE_STATIONARY := 0;
const STATE_MOVING := 1;

var done := 0;
var state;
var relative_direction;
var boat;
var tillerman;

function handle_speech( event )
    var text := lower( event.text );
    if (text["forward"] || text["unfurl sail"] || text["unfurl"])
        state := STATE_MOVING;
        relative_direction := 0;
        PrintTextAbove( tillerman, "Aye, unfurling sails, Sir" );
    elseif (text["backward"])
        state := STATE_MOVING;
        relative_direction := 4;
        PrintTextAbove( tillerman, "Aye, Sir" );
    elseif (text["stop"] || text["furl sail"] || text["furl"])
        state := STATE_STATIONARY;
        PrintTextAbove( tillerman, "Aye, anchors down, Sir" );
    elseif (text["turn right"] || text["starboard"])
        TurnBoat( boat, 1 );
        PrintTextAbove( tillerman, "Aye, Sir" );
        sleep(1);
    elseif (text["turn left"] || text["port"])
        TurnBoat( boat, 3 );
        PrintTextAbove( tillerman, "Aye, Sir" );
        sleep(1);
    elseif (text["come about"])
        TurnBoat( boat, 2 );
        PrintTextAbove( tillerman, "Aye, Sir, coming about" );
        sleep(1);
    elseif (text["exit"])
        PrintTextAbove( tillerman, "I'll be taking the day off, Sir" );
        done := 1;
    endif
endfunction

function continue_operation()
    if (state == STATE_MOVING)
        MoveBoatRelative( boat, relative_direction );
    endif
endfunction

function boat_script()
    RegisterForSpeechEvents( tillerman, 10 );

    while (!done)
        var event := wait_for_event( 0 );
        if (event)
            if ((event.type == "speech") || (event.type == "Speech"))
                handle_speech( event );
            endif
        endif
       
        continue_operation();
        sleep(1);
    endwhile
endfunction

program textcmd_boat( who )
    SendSysMessage( who, "Target the boat." );
    boat := Target( who );

    if (!boat)
        return;
    endif

    tillerman := boat.tillerman;
    if (!tillerman)
        SendSysMessage( who, "There was no tillerman." );
        return;
    endif

    boat_script();
endprogram